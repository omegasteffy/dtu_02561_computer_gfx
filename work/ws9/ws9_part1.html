<html>
    <title>Worksheet 9 - Part 1</title>

    <!-- This shader pair is used to draw a textures and etc.-->
    <script id="vert_for_ground" type="x-shader/x-vertex" >
	    attribute vec4   a_Position;
       // attribute vec2   a_texCoordinate;
        uniform mat4 trsMatrix;
        uniform mat4 proj_Matrix;
        uniform mat4 camera_Matrix;
        uniform mat4 lightProjMatrix;
        uniform mat4 lightCamMatrix;
        varying vec2 v_texCoordinate;  
        varying vec4 v_PositionFromLight;   

	    void main()
        {

            gl_Position = proj_Matrix * camera_Matrix * trsMatrix * a_Position;
            v_PositionFromLight = lightProjMatrix * lightCamMatrix * trsMatrix * a_Position;
            v_texCoordinate = a_Position.xz;
            gl_PointSize= 20.0;
	    }
    </script>
    <script id="frag_for_ground" type="x-shader/x-fragment" >
	    precision mediump float;
        varying  vec2 v_texCoordinate;
        varying vec4 v_PositionFromLight;  
        uniform sampler2D tex1;
        uniform sampler2D shadow_map;
        uniform bool is_a_shadow;
        
	    void main()
	    {
            vec3 shadowCoord = (v_PositionFromLight.xyz/v_PositionFromLight.w)/2.0 + 0.5;
            vec4 rgbaDepth = texture2D(shadow_map, shadowCoord.xy);
            float depth = rgbaDepth.r; // Retrieve the z-value from R
            float visibility = (shadowCoord.z > depth + 0.005) ? 0.4 : 1.0;
            gl_FragColor = texture2D(tex1,v_texCoordinate);
            gl_FragColor.rgb = visibility *gl_FragColor.rgb;
            gl_FragColor.a = 0.7; //to allow for reflections
            if(is_a_shadow)
            {
                gl_FragColor = vec4(0,0,0,1.0);
            }
            //gl_PointSize= 20.0;
	    }
    </script>
    <script id="vert_for_obj" type="x-shader/x-vertex" >
        attribute vec4   a_Position;
        attribute vec4   a_Color;
        attribute vec4   a_Normal;
        varying vec4    v_Color;
        varying vec4    v_Normal;
        uniform mat4    trsMatrix;
        uniform mat4    proj_Matrix;
        uniform mat4    camera_Matrix;
        uniform mat4    lightProjMatrix;
        uniform mat4    lightCamMatrix;
        uniform mat4    reflection_matrix;
        varying vec4    v_PositionFromLight;   
        
        uniform vec4 lightPos;

        varying vec3 N;
        varying vec3 L;
        varying vec3 E;

	    void main()
        {
            vec4 normal_mod = a_Normal;
            normal_mod.w = 0.0;
            vec3 vertex_eyePos_pos = (camera_Matrix*trsMatrix*a_Position).xyz;
            L = normalize( lightPos.xyz - vertex_eyePos_pos ); //light from a position
            //L = normalize( camera_Matrix * lightDirection ).xyz; //light from a distant (only direction)
            E = -(camera_Matrix*trsMatrix*a_Position).xyz; //Should the TRS matrix be removed ?
   
            N = normalize((camera_Matrix*trsMatrix*normal_mod).xyz); //normalized Normal vector
            v_PositionFromLight = lightProjMatrix* lightCamMatrix* trsMatrix * a_Position;
            gl_Position = proj_Matrix  *camera_Matrix  * trsMatrix *reflection_matrix* a_Position;
            v_Color = a_Color;
            v_Normal = a_Normal;
	    }
    </script>
    <script id="frag_for_obj" type="x-shader/x-fragment" >
	    precision mediump float;
        varying vec4    v_Color;
        varying vec4    v_Normal;
        uniform bool    is_a_shadow; //used for shadow projection
        uniform bool    disable_shadow_map;
        varying vec4    v_PositionFromLight;
        uniform sampler2D shadow_map;

        varying vec3 N,L,E;
	    void main()
	    {
            vec3 shadowCoord = (v_PositionFromLight.xyz/v_PositionFromLight.w)/2.0 + 0.5;
            vec4 rgbaDepth = texture2D(shadow_map, shadowCoord.xy);
            float depth = rgbaDepth.r; // Retrieve the z-value from R
            float visibility = (shadowCoord.z > depth + 0.005) ? 0.2: 1.0;
            visibility = disable_shadow_map ? 1.0 : visibility;

            float shinyness = 30.0;
            if(is_a_shadow)
            {
                gl_FragColor = vec4( 0.0 , 0.0 , 0.0 ,1.0);
                return;
            }
            float specular_coef = 0.4;
            float ambient_coef =  0.1;
            float diffuse_coef = 0.6;

            vec4 diffuseColor = vec4(1.0,1.0,1.0,1.0);
            vec4 ambientColor = diffuseColor;
            vec4 specularColor = diffuseColor;

            
            vec3 N_norm = normalize(N);
            vec3 L_norm = normalize(L);
            vec3 E_norm = normalize(E);
            //calculation of diffuse light intensity is simply the dot-product
            float Kd = max(dot(L_norm,N_norm),0.0); // but never negative
            vec4  diffuse = Kd *v_Color* diffuseColor;

            //calculation of specular light
            vec3  H = normalize(L_norm+E_norm);//half-way vector?
            float Ks = pow( max(dot(N_norm,H),0.0) , shinyness);
            vec4  specular = Ks* specularColor; 

            if( dot(L_norm,N_norm) < 0.0 )
            {
              specular  = vec4(0.0 , 0.0 , 0.0 , 1.0);
            }

            gl_FragColor = (ambientColor*ambient_coef) +(diffuse*diffuse_coef*visibility) + specular*specular_coef*visibility;

	    }
    </script>
    <script id="vert_for_lightdepth" type="x-shader/x-vertex" >
        attribute vec4   a_Position;
        uniform mat4 trsMatrix;
        uniform mat4 proj_Matrix;
        uniform mat4 camera_Matrix;
	    void main()
        {
            gl_Position = proj_Matrix * camera_Matrix * trsMatrix * a_Position;
	    }
    </script>
    <script id="frag_for_lightdepth" type="x-shader/x-fragment" >
	    precision mediump float;        
	    void main()
	    {
            gl_FragColor = vec4(gl_FragCoord.z, 0.0, 0.0, 1.0); // Write the z-value in R
	    }
    </script>

<body>
    <table><tr>
                <td>options</td>
    </tr>

                <tr><td>
                    <input type="checkbox" id="move_teapot"> Move teapot up/down
                </td></tr>
                <tr><td>
                        <input type="checkbox" id="rotate_teapot"> Rotate Teapot
                </td></tr>
                <tr><td>
                        <input type="checkbox" id="rotate_light" checked> Rotate Light source
                </td></tr>
                <tr><td>
                    <input type="checkbox" id="camera_above"> Camera look strait down
                </td></tr>
                <tr><td>
                    <input type="checkbox" id="view_light_depth"> View depth seen from light
                </td></tr>
                <tr><td>
                    <input type="checkbox" id="disable_teapot_shadowmapping" checked> Do not apply shadow mapping on teapot
                </td></tr>
                <tr><td>
                    <input type="checkbox" id="draw_shadow_projection_ground"> Draw the shadow projection on the ground
                </td></tr>
                
                
                

        </table>
	<canvas id='draw_area' height="512" width="512" />
    <script src="../angel_common/initShaders.js"></script>
    <script src="../angel_common/webgl-utils.js"></script>
    <script src="../angel_common/MV.js"></script>
    <script src="../other_common/helpers.js"></script>
    <script src="../other_common/OBJParser.js"></script>
    <script src="../other_common/OBJLoadingHelper.js"></script>
    <script src="ws9_part1.js"></script>
</body>

</html>
