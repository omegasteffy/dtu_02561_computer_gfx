<html>
	<title>Final Project</title>

    <script id="simple_vert" type="x-shader/x-vertex" >
	    attribute vec4   a_Position;
        uniform mat4 trsMatrix;
        uniform mat4 proj_Matrix;
        uniform mat4 camera_Matrix;
        varying  vec4 v_color;
	    void main()
        {

            gl_Position = proj_Matrix * camera_Matrix * trsMatrix * a_Position;
            v_color.rg = a_Position.xy;
	    }
    </script>
    <script id="simple_frag" type="x-shader/x-fragment" >
	    precision mediump float;
        varying  vec4 v_color;
        
	    void main()
	    {
            gl_FragColor = vec4(1.0,1.0,1.0,0.5);
            gl_FragColor.rgb =v_color.rgb;
	    }
    </script>
    <script id="vert" type="x-shader/x-vertex" >

        //The hash and noise function is taken from https://www.shadertoy.com/view/Msf3WH
    vec2 hash( vec2 p ) // replace this by something better
    {
        p = vec2( dot(p,vec2(127.1,311.7)),
                dot(p,vec2(269.5,183.3)) );

        return -1.0 + 2.0*fract(sin(p)*43758.5453123);
    }

    float noise( in vec2 p )
    {
        const float K1 = 0.366025404; // (sqrt(3)-1)/2;
        const float K2 = 0.211324865; // (3-sqrt(3))/6;

        vec2 i = floor( p + (p.x+p.y)*K1 );
        
        vec2 a = p - i + (i.x+i.y)*K2;
        vec2 o = step(a.yx,a.xy);    
        vec2 b = a - o + K2;
        vec2 c = a - 1.0 + 2.0*K2;

        vec3 h = max( 0.5-vec3(dot(a,a), dot(b,b), dot(c,c) ), 0.0 );

        vec3 n = h*h*h*h*vec3( dot(a,hash(i+0.0)), dot(b,hash(i+o)), dot(c,hash(i+1.0)));

        return dot( n, vec3(70.0) );
        
    }
    const float SEA_LEVEL = 0.0;
    const float LOWER_OFFSET =0.30;
    float noise_sum(in vec2 coord,in float time)
    {
        float noise_res  = LOWER_OFFSET+ 0.5*noise(vec2(coord.x,coord.y+time)) ;
        noise_res       += 0.25*noise(vec2(coord.x*2.0,(coord.y+time)*2.0)) ;
        noise_res       += 0.125*noise(vec2(coord.x*4.0,(coord.y+time)*4.0)) ;
        noise_res       += .5*0.125*noise(vec2(coord.x*8.0,(coord.y+time)*8.0)) ;
        return clamp(noise_res,SEA_LEVEL,10.0); //only clamp lower
    }

    float noise_and_normal(in vec2 pos,in float time,in float gradient_step,out vec3 normal)
    {
        vec3 p0,p1,p2;
        p0.xz = pos;
        p1.xz = vec2(pos.x + gradient_step  ,  pos.y);  //a step in x-dir                 ..increase => normal.x increase /red
        p2.xz = vec2(pos.x   ,  pos.y + gradient_step); //a step in y-dir (z-world-coord) ..increase => normal.z increase /blue

        p0.y  = noise_sum( p0.xz , time );
        p1.y  = noise_sum( p1.xz , time );
        p2.y  = noise_sum( p2.xz , time );

        vec3 v1 = p1-p0;
        vec3 v2 = p2-p0;
        normal = cross(v1,v2);
       
        return p0.y;
    }
        uniform float time;
	    attribute vec4   a_Position;
        uniform mat4 trsMatrix;
        uniform mat4 proj_Matrix;
        uniform mat4 camera_Matrix;
        varying  vec3 v_normal;
        varying float raw_height;
        varying vec2 original_xy_coord;
        

        //light-stuff
        varying vec3 N,L,E;
        uniform vec4 light_pos;
        
	    void main()
        {
            vec4  Pos = a_Position.xzyw;
            original_xy_coord = a_Position.xy;
            float noise_res = noise_and_normal(a_Position.xy,time,0.05,v_normal);
            v_normal = -v_normal;
            Pos.y  = noise_res; //Apply noise sample as height
            raw_height =noise_res;

            //we got final pos and can calculate light variables
            vec3 vertex_pos_camSpace = (camera_Matrix*trsMatrix*Pos).xyz;
            vec3 light_pos_camSpace = (camera_Matrix*light_pos).xyz;

            vec4 normal4d;
            normal4d.w = 0.0;
            normal4d.xyz = v_normal;
            L = normalize( light_pos_camSpace - vertex_pos_camSpace ); // light from a specific point
            E = -vertex_pos_camSpace; //away from the vertex toward origo... the camera/eye
            N = normalize((camera_Matrix*trsMatrix*normal4d).xyz); //normalized Normal vector

            gl_Position = proj_Matrix * camera_Matrix * trsMatrix *Pos;
	    }
    </script>
    <script id="frag" type="x-shader/x-fragment" >  
        precision highp float;
        

        vec2 hash( vec2 p ) // replace this by something better
        {
            p = vec2( dot(p,vec2(127.1,311.7)),
                    dot(p,vec2(269.5,183.3)) );
    
            //return -1.0 + 2.0*fract(sin(p)*43758.5453123);
            return -1.0 + 2.0*fract(sin(p)*9999.5453123); //just make sure the seed is somewhat different from vertex shader
        }
    
        float noise( in vec2 p )
        {
            const float K1 = 0.366025404; // (sqrt(3)-1)/2;
            const float K2 = 0.211324865; // (3-sqrt(3))/6;
    
            vec2 i = floor( p + (p.x+p.y)*K1 );
            
            vec2 a = p - i + (i.x+i.y)*K2;
            vec2 o = step(a.yx,a.xy);    
            vec2 b = a - o + K2;
            vec2 c = a - 1.0 + 2.0*K2;
    
            vec3 h = max( 0.5-vec3(dot(a,a), dot(b,b), dot(c,c) ), 0.0 );
    
            vec3 n = h*h*h*h*vec3( dot(a,hash(i+0.0)), dot(b,hash(i+o)), dot(c,hash(i+1.0)));
    
            return dot( n, vec3(70.0) );
            
        }
        varying  vec3 v_normal;
        varying  vec3 N,L,E;
        varying float raw_height;
        uniform float time;

        const float LEVEL_SEA = 0.00;
        const float LEVEL_RIGHT_BENEATH_MOUNTAIN = 0.42;
        const float LEVEL_MOUNTAIN = 0.50;
        const float LEVEL_MOUNTAIN_SNOW = 0.65;
        const float LEVEL_PRECISION_SLACK = 0.01;
        const vec4 COLOR_GRASS = vec4(.4,.9,0.2,1.0);
        const vec4 COLOR_SAND = vec4(.8,.6,0.1,1.0);
        const vec4 COLOR_MOUNTAIN = vec4(.6,.6,0.6,1.0);
        const vec4 COLOR_SNOW = vec4(.97,.97,0.97,1.0);
        const vec4 COLOR_WATER = vec4(.1,.1,0.9,1.0);

        vec4 specularColor  = vec4(1.0,0.8,0.8,1.0); // red-ish tone
        vec4 diffuseColor   = vec4(1.0,1.0,1.0,1.0);
        vec4 ambientColor   = vec4(0.0,0.0,0.1,1.0);

        float shinyness = 3.0;
        
        float specular_coef = 0.0;
        float ambient_coef  = 0.25;
        float diffuse_coef  = 0.55;

        varying vec2 original_xy_coord; // used for sampling grass

        vec4 determine_color(out float specular_coefficient)
        {

            if( (raw_height - LEVEL_PRECISION_SLACK) <= LEVEL_SEA)
            {
               return COLOR_WATER; //Bluish
            }
            if( raw_height >= LEVEL_MOUNTAIN_SNOW )
            {
                specular_coefficient=.3;
                return COLOR_SNOW; //white
            }
            if( raw_height >= LEVEL_MOUNTAIN )
            {
                specular_coefficient=.1;
                return COLOR_MOUNTAIN; //grey
            }
            if( raw_height >= LEVEL_MOUNTAIN )
            {
                specular_coefficient=.1;
                return COLOR_MOUNTAIN; //grey
            }
            if( raw_height >= LEVEL_RIGHT_BENEATH_MOUNTAIN )
            {
                return COLOR_SAND; //grey
            }
            float noise_sample = noise(vec2(original_xy_coord.x*4.0,(original_xy_coord.y+time)*4.0)); //4x higher frequency
            const float grass_dominace  = 0.5; //zero means equal amount of grass/sand. Positive => more grass.
            const float slope_factor = 2.0; // higher slope factor =>fewer areas of mixed grass/sand ... more area will be clamped
            float grass_factor = clamp( slope_factor *(noise_sample+grass_dominace) ,0.0 , 1.0 );
            return mix(COLOR_SAND,COLOR_GRASS,grass_factor);
        }
	    void main()
	    {
            vec3 N_norm = normalize(N);
            vec3 L_norm = normalize(L);
            vec3 E_norm = normalize(E);


            vec4 point_color = determine_color(specular_coef);

            //float Kd = max(dot(L_norm,N_norm),0.0); // but never negative
            float Kd = pow(max(dot(L_norm,N_norm),0.0),0.6); // but never negative
            vec4  diffuse = Kd *diffuseColor*point_color;
    
            //calculation of specular light is 
            vec3  H = normalize(L_norm+E_norm);
            float Ks = pow( max(dot(N_norm,H),0.0) , shinyness);
            vec4  specular = Ks* specularColor;
            
            if( dot(L_norm,N_norm) < 0.0 )
            {
                specular  = vec4(0.0 , 0.0 , 0.0 , 1.0);
            }
    
    
            gl_FragColor = (point_color*ambient_coef) +diffuse*diffuse_coef + specular*specular_coef;

        }
        
      
    </script>
<body>
    <tr>
            <td>Enabled scrolling</td>
            <td><input type="checkbox" id="enable_scroll" checked></td>
    </tr><br/>
	<canvas id='draw_area' height="512" width="512" />
    <script src="../angel_common/initShaders.js"></script>
    <script src="../angel_common/webgl-utils.js"></script>
    <script src="../angel_common/MV.js"></script>
    <script src="../other_common/helpers.js"></script>
    <script src="plane3d.js"></script>
    <script src="final_part8_random_grass.js"></script>
</body>

</html>
